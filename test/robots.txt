*** Settings ***
Library  Selenium2Library
Test Setup  Open Browser  http://localhost:4567/
Test Teardown  Close Browser

*** Test Cases ***
Create a person
  [template]  create a person
  Joseph                      Coach     Success
  Jackson                     ${empty}  Success
  ${empty}                    Coach     Name is required
  Name Longer Than 20 chars   Coach     Name is too long
  3Joseph                     Coach     Name should start with alphabet

Name should not be duplicated
  Given there is an existing person  Joseph  Coach
  When create a person with  Joseph  Worker
  Then fail with  Name should not be duplicated

*** Keywords ***
create a person
  [Arguments]  ${name}  ${title}  ${result}
  When create a person with  ${name}  ${title}
  run keyword if  '${result}'=='Success'  success with  ${name}  ${title}
  run keyword unless  '${result}'=='Success'  fail with  ${result}

create a person with
  [Arguments]  ${name}  ${title}
  go to  http://localhost:4567/
  input text  name=name  ${name}
  input text  name=title  ${title}
  submit form

there is an existing person
  [Arguments]  ${name}  ${title}
  create a person with  ${name}  ${title}

success with
  [Arguments]  ${name}  ${title}
  page should contain  ${name}
  page should contain  ${title}

fail with
  [Arguments]  ${result}
  page should contain  ${result}
